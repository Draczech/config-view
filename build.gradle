plugins {
  id 'com.github.spotbugs' version '4.0.8'
  id "com.diffplug.gradle.spotless" version "3.30.0"
  id 'net.researchgate.release' version '2.8.1'
}

ext {
  cglibVersion = '3.2.6'
  configVersion = '1.4.0'
  junitJupiterVersion = '5.6.0'
}

group = 'cz.datadriven.utils'

apply plugin: 'idea'
apply plugin: 'java'
apply from: "${rootProject.projectDir}/gradle/publishing.gradle"

repositories {
  mavenCentral()
}

dependencies {
  implementation "cglib:cglib:${cglibVersion}"
  implementation "com.typesafe:config:${configVersion}"
  compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

spotless {
  java {
    licenseHeaderFile 'HEADER'
    googleJavaFormat("1.7")
  }
}

spotbugsMain {
  effort = 'max'
  reportLevel = 'low'
  reports {
    xml {
      enabled = false
    }
    html {
      enabled = true
    }
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

release {
  tagTemplate = 'v$version'
}

test {
  useJUnitPlatform()
}
